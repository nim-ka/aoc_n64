/* Super Mario 64 (J) linker script
 * generated by n64split v0.4a - N64 ROM splitter */

OUTPUT_ARCH (mips)

__segPoolStart = 0x8005C000; /* the segment pool starts here. */
__segCodeSection = 0x8016F000;
__segPoolEnd = 0x801C1000;
__rdramStart = 0x8038F800;
__expansionRamStart = 0x80400000;

#define BEGIN_SEG(name, addr) \
    _##name##SegmentStart = ADDR(.name); \
    _##name##SegmentRomStart = __romPos; \
    .name addr : AT(__romPos)

#define END_SEG(name) \
    _##name##SegmentEnd = ADDR(.name) + SIZEOF(.name); \
    _##name##SegmentRomEnd = __romPos + SIZEOF(.name); \
    __romPos += SIZEOF(.name); \

#define BEGIN_BSS(name) \
    _##name##SegmentBssStart = ADDR(.name.bss); \
    .name.bss (NOLOAD) :

#define END_BSS(name) \
    _##name##SegmentBssEnd = ADDR(.name.bss) + SIZEOF(.name.bss);

SECTIONS
{
    __romPos = 0;

   BEGIN_SEG(boot, 0x04000000)
   {
      build/asm/rom_header.o(.text);
      build/asm/boot.o(.text);
   }
   END_SEG(boot)

   BEGIN_SEG(main, 0x80246000)
   {
      build/asm/entry.o(.text);
      build/src/main.o(.text);
      build/src/display.o(.text);
      build/src/game.o(.text);
      build/src/sound_init.o(.text);
      build/src/level_update.o(.text);
      build/src/interaction.o(.text);
      build/asm/mario.o(.text);
      build/src/mario_step.o(.text);
      build/asm/mario_actions_cutscene.o(.text);
      build/src/mario_actions_automatic.o(.text);
      build/asm/mario_actions_stationary.o(.text);
      build/asm/mario_actions_moving.o(.text);
      build/src/mario_actions_airborne.o(.text);
      build/src/mario_actions_submerged.o(.text);
      build/asm/mario_actions_object.o(.text);
      build/asm/castle_message_behaviors.o(.text);
      build/src/memory.o(.text);
      build/src/save_file.o(.text);
      build/src/rendering.o(.text);
      build/asm/resource_meter.o(.text);
      build/asm/decompress.o(.text);
      build/src/camera.o(.text);
      build/asm/camera.o(.text);
      build/asm/object_list_processor.o(.text);
      build/asm/level_functions.o(.text);
      build/src/platform_displacement.o(.text);
      build/src/object_collision.o(.text);
      build/src/spawn_object.o(.text);
      build/src/mapinfo_1.o(.text);
      build/src/mapinfo_2.o(.text);
      build/asm/screen_transition.o(.text);
      build/asm/rotate.o(.text);
      build/src/skybox.o(.text);
      build/asm/moving_texture.o(.text);
      build/src/transparent_texture.o(.text);
      build/src/print.o(.text);
      build/asm/ingame_menu.o(.text);
      build/src/hud.o(.text);
      build/src/hud_print.o(.text);
      build/asm/obj_behaviors.o(.text);
      build/asm/obj_behaviors_2.o(.text);
      build/asm/audio_dac.o(.text);
      build/asm/audio_dma.o(.text);
      build/asm/audio_playback.o(.text);
      build/asm/audio_interface_1.o(.text);
      build/asm/audio_interface_2.o(.text);
#if VERSION_US
      . += 0xD90;
#endif
      build/asm/libultra.o(.text);
      /* data */
      build/data/data_8032B3F0.o(.data*);
#if VERSION_US
      . += 0x10;
#endif
      build/src/main.o(.data*);
      build/src/display.o(.data*);
      build/src/game.o(.data*);
      build/src/sound_init.o(.data*);
      build/src/level_update.o(.data*);
      build/src/interaction.o(.data*);
      build/data/mario.data.o(.data*);
      build/src/mario_step.o(.data*);
      build/data/mario_actions_cutscene.data.o(.data*);
      build/data/mario_actions_moving.data.o(.data*);
      build/src/mario_actions_submerged.o(.data*);
      build/data/mario_actions_object.data.o(.data*);
      build/data/castle_message_behaviors.data.o(.data*);
      build/src/memory.o(.data*);
      build/src/save_file.o(.data*);
      build/data/save_file.data.o(.data*);
      build/src/rendering.o(.data*);
      build/data/rendering.data.o(.data*);
      build/data/resource_meter.data.o(.data*);
      build/data/camera_before.data.o(.data*);
      build/src/camera.o(.data*);
      build/data/camera.data.o(.data*);
      build/src/platform_displacement.o(.data*);
      build/src/mapinfo_1.o(.data*);
      build/src/mapinfo_2.o(.data*);
      build/data/screen_transition.data.o(.data*);
      build/data/rotate.data.o(.data*);
      build/src/skybox.o(.data*);
      build/data/moving_texture.data.o(.data*);
      build/src/transparent_texture.o(.data*);
      build/src/print.o(.data*);
      build/data/ingame_menu.data.o(.data*);
      build/src/hud.o(.data*);
      build/src/hud_print.o(.data*);
      build/data/obj_behaviors_1.data.o(.data*);
      build/data/obj_behaviors_2.data.o(.data*);
      build/data/audio.data.o(.data*);
      build/data/libultra.data.o(.data*);
      /* rodata */
      build/src/main.o(.rodata*);
      build/src/game.o(.rodata*);
      build/src/sound_init.o(.rodata*);
      build/src/level_update.o(.rodata*);
      build/src/interaction.o(.rodata*);
      build/data/mario.rodata.o(.rodata*);
      build/src/mario_step.o(.rodata*);
      build/data/mario_actions_cutscene.rodata.o(.rodata*);
      build/src/mario_actions_automatic.o(.rodata*);
      build/data/mario_actions_stationary.rodata.o(.rodata*);
      build/data/mario_actions_moving.rodata.o(.rodata*);
      build/src/mario_actions_airborne.o(.rodata*);
      build/src/mario_actions_submerged.o(.rodata*);
      build/data/mario_actions_object.rodata.o(.rodata*);
      build/data/castle_message_behaviors.rodata.o(.rodata*);
      build/src/rendering.o(.rodata*);
      build/src/camera.o(.rodata*);
      build/data/camera.rodata.o(.rodata*);
      build/data/level_functions.rodata.o(.rodata*);
      build/src/object_collision.o(.rodata*);
      build/src/spawn_object.o(.rodata*);
      build/src/mapinfo_1.o(.rodata*);
      build/src/mapinfo_2.o(.rodata*);
      build/data/screen_transition.rodata.o(.rodata*);
      build/data/rotate.rodata.o(.rodata*);
      build/src/skybox.o(.rodata*);
      build/data/moving_texture.rodata.o(.rodata*);
      build/src/print.o(.rodata*);
      build/data/ingame_menu.rodata.o(.rodata*);
      build/src/hud.o(.rodata*);
      build/src/hud_print.o(.rodata*);
      build/data/obj_behaviors_1.rodata.o(.rodata*);
      build/data/obj_behaviors_2.rodata.o(.rodata*);
      build/data/audio.rodata.o(.rodata*);
   }
   END_SEG(main)
   BEGIN_BSS(main)
   {
#if VERSION_US
      . += 0x430;
#endif
      build/src/main.o(.bss*);
      build/src/game.o(.bss*);
      build/data/bss1.o(.bss*);
      build/src/level_update.o(.bss*);
      build/data/bss80339F00.o(.bss*);
      build/src/mario_actions_submerged.o(.bss*);
      build/data/bss80339FE0.o(.bss*);
      build/src/hud_print.o(.bss*);
      build/data/bss803600E0.o(.bss*);
   }
   END_BSS(main)

   BEGIN_SEG(engine, 0x80378800)
   {
      build/src/math_util.o(.text);
      build/src/graph_node.o(.text);
      build/src/geo_layout.o(.text);
      build/asm/level_script.o(.text);
      build/src/surface_collision1.o(.text);
      build/src/surface_collision2.o(.text);
      build/src/behavior_script.o(.text);
      /* data */
      build/data/graph_node.data.o(.data*);
      build/data/math_util.data.o(.data*);
      build/src/geo_layout.o(.data*);
      build/data/level_script.data.o(.data*);
      build/src/behavior_script.o(.data*);
      /* rodata */
      build/src/math_util.o(.rodata*);
      build/data/level_script.rodata.o(.rodata*);
      build/src/surface_collision1.o(.rodata*);
      build/src/surface_collision2.o(.rodata*);
   }
   END_SEG(engine)
   BEGIN_BSS(engine)
   {
      build/data/bss2.o(.bss*);
   }
   END_BSS(engine)

   /*
    RDRAM begins at 0x8038F800 and ends where
    expansion RAM begins at 0x80400000;
   */
   . = __rdramStart;

   BEGIN_BSS(rdram)
   {
      build/data/rdram.o(.bss*);
   }
   END_BSS(rdram)
   __rdramEnd = .;
   PROVIDE(rdramEnd = .);

   ASSERT((rdramEnd <= 0x80400000), "Error: RDRAM expanded into Expansion RAM, despite Expansion RAM not being defined.")

   /* load MIO0 and level data at 0x001076A0 */
   BEGIN_SEG(mio0, __romPos)
   {
      build/data/level_actors.o(.mio0);
   }
   END_SEG(mio0)

   /* use segmented addressing for behaviors */
   BEGIN_SEG(behavior, 0x13000000)
   {
      build/data/behavior_data.o(.behavior);
   }
   END_SEG(behavior)

   /* 0x8016F000 21D7D0-255EC0 [386F0] */
   BEGIN_SEG(goddard, __segCodeSection)
   {
      build/src/level_select_menu.o(.text);
      build/src/intro_geo.o(.text);
      build/asm/file_select.o(.text);
      build/src/star_select.o(.text);
      build/asm/debug_memory.o(.text);
      build/src/game_over_1.o(.text);
      build/asm/game_over_2.o(.text);
      build/asm/press_start_head_1.o(.text);
      build/asm/press_start_head_2.o(.text);
      build/asm/press_start_head_3.o(.text);
      build/asm/press_start_head_4.o(.text);
      build/asm/press_start_head_5.o(.text);
      build/asm/press_start_head_6.o(.text);
      /* data, rodata, per file */
      build/src/level_select_menu.o(.data*);
      build/src/level_select_menu.o(.rodata*);
      build/src/intro_geo.o(.data*);
      build/data/file_select.data.o(.data*);
      build/data/file_select.rodata.o(.rodata*);
      build/src/star_select.o(.data*);
      build/src/star_select.o(.rodata*);
      build/data/rodata_801A8050.o(.rodata*);
   }
   END_SEG(goddard)
   BEGIN_BSS(goddard)
   {
      build/data/bss3.o(.bss*);
   }
   END_BSS(goddard)

   .stack (NOLOAD) :
   {
      build/data/stack.o(.stack);
   }

   /* 0x268020 0x268020-0 [0] */
   BEGIN_SEG(mio0_c, __romPos)
   {
      build/data/level_misc.o(.mio0_c);
   }
   END_SEG(mio0_c)

   /* 271B20-2A42A0 [32780] */
   BEGIN_SEG(text271B20, __romPos)
   {
      build/data/unknown_271B20.o(.text);
   }
   END_SEG(text271B20)

   /* 0x2A42A0 0x2A42A0-0 [0] */
   BEGIN_SEG(mio0_b, __romPos)
   {
      build/data/level_main.o(.mio0_b);
   }
   END_SEG(mio0_b)

   /* 4E9FA0-? [?] */
   BEGIN_SEG(text4E9FA0, __romPos)
   {
      build/data/unknown_4E9FA0.o(.text);
      build/data/sound_data.o(.text);
   }
   END_SEG(text4E9FA0)

   /* Discard everything not specifically mentioned above. */
   /DISCARD/ :
   {
      *(*);
   }
}
