/* Super Mario 64 (J) linker script
 * generated by n64split v0.4a - N64 ROM splitter */

OUTPUT_ARCH (mips)

__segPoolStart = 0x8005C000; /* the segment pool starts here. */
__segCodeSection = 0x8016F000;
__segPoolEnd = 0x801C1000;
__rdramStart = 0x8038F800;
__expansionRamStart = 0x80400000;

#define BEGIN_SEG(name, addr) \
    _##name##SegmentStart = ADDR(.name); \
    _##name##SegmentRomStart = __romPos; \
    .name addr : AT(__romPos)

#define END_SEG(name) \
    _##name##SegmentEnd = ADDR(.name) + SIZEOF(.name); \
    _##name##SegmentRomEnd = __romPos + SIZEOF(.name); \
    __romPos += SIZEOF(.name);

#define BEGIN_NOLOAD(name) \
    _##name##SegmentNoloadStart = ADDR(.name.noload); \
    .name.noload (NOLOAD) :

#define END_NOLOAD(name) \
    _##name##SegmentNoloadEnd = ADDR(.name.noload) + SIZEOF(.name.noload);

#define MIO0_SEG(name, segAddr) \
   BEGIN_SEG(name##_mio0, segAddr) \
   { \
      build/mio0/name.mio0.o(.data); \
      . = ALIGN(0x10); \
   } \
   END_SEG(name##_mio0)

#define STANDARD_LEVEL(name) \
   BEGIN_SEG(name##_segment_7, 0x07000000) \
   { \
      build/levels/name.o(.seg07); \
   } \
   END_SEG(name##_segment_7) \
   BEGIN_SEG(name, 0x0E000000) \
   { \
      build/levels/name.o(.level); \
   } \
   END_SEG(name)

#define STANDARD_OBJECTS(name, segAddr, geoAddr) \
   MIO0_SEG(name, segAddr) \
   BEGIN_SEG(name##_geo, geoAddr) \
   { \
      build/geo/name.o(.geo); \
      . = ALIGN(0x10); \
   } \
   END_SEG(name##_geo)

SECTIONS
{
   __romPos = 0;

   BEGIN_SEG(boot, 0x04000000)
   {
      build/asm/rom_header.o(.text);
      build/asm/boot.o(.text);
   }
   END_SEG(boot)

   BEGIN_SEG(main, 0x80246000)
   {
      build/asm/entry.o(.text);
      build/src/main.o(.text);
      build/src/display.o(.text);
      build/src/game.o(.text);
      build/src/sound_init.o(.text);
      build/src/level_update.o(.text);
      build/src/interaction.o(.text);
      build/asm/mario.o(.text);
      build/src/mario_step.o(.text);
      build/asm/mario_actions_cutscene.o(.text);
      build/src/mario_actions_automatic.o(.text);
      build/asm/mario_actions_stationary.o(.text);
      build/src/mario_actions_moving.o(.text);
      build/src/mario_actions_airborne.o(.text);
      build/src/mario_actions_submerged.o(.text);
      build/src/mario_actions_object.o(.text);
      build/src/castle_message_behaviors.o(.text);
      build/src/memory.o(.text);
      build/src/save_file.o(.text);
      build/src/area.o(.text);
      build/src/rendering_graph_node.o(.text);
      build/src/profiler.o(.text);
      build/asm/decompress.o(.text);
      build/src/camera.o(.text);
      build/asm/camera.o(.text);
      build/src/object_list_processor.o(.text);
      build/src/object_helpers.o(.text);
      build/asm/object_helpers.o(.text);
      build/asm/behavior_actions.o(.text);
      build/src/platform_displacement.o(.text);
      build/src/object_collision.o(.text);
      build/src/spawn_object.o(.text);
      build/src/spawn_sound.o(.text);
      build/src/debug.o(.text);
      build/asm/screen_transition.o(.text);
      build/src/shadow.o(.text);
      build/src/skybox.o(.text);
      build/src/moving_texture.o(.text);
      build/src/transparent_texture.o(.text);
      build/src/paintings.o(.text);
      build/src/print.o(.text);
      build/asm/ingame_menu.o(.text);
      build/asm/ingame_menu_2.o(.text);
      build/src/hud.o(.text);
      build/src/hud_print.o(.text);
      build/src/obj_behaviors.o(.text);
      build/asm/obj_behaviors.o(.text);
      build/asm/obj_behaviors_2.o(.text);
      build/asm/audio_something.o(.text);
      build/asm/audio_dac.o(.text);
      build/asm/audio_dma.o(.text);
      build/asm/audio_playback.o(.text);
      build/asm/audio_interface_1.o(.text);
      build/asm/audio_interface_2.o(.text);
#if VERSION_US
      . += 0xD90;
#endif
      build/asm/libultra.o(.text);
      /* data */
      build/data/data_8032B3F0.o(.data*);
#if VERSION_US
      . += 0x10;
#endif
      build/src/main.o(.data*);
      build/src/display.o(.data*);
      build/src/game.o(.data*);
      build/src/sound_init.o(.data*);
      build/src/level_update.o(.data*);
      build/src/interaction.o(.data*);
      build/data/mario.data.o(.data*);
      build/src/mario_step.o(.data*);
      build/data/mario_actions_cutscene.data.o(.data*);
      build/src/mario_actions_moving.o(.data*);
      build/src/mario_actions_submerged.o(.data*);
      build/src/mario_actions_object.o(.data*);
      build/src/castle_message_behaviors.o(.data*);
      build/src/memory.o(.data*);
      build/src/save_file.o(.data*);
      build/data/save_file.data.o(.data*);
      build/src/area.o(.data*);
      build/src/rendering_graph_node.o(.data*);
      build/data/rendering_graph_node.data.o(.data*);
      build/src/profiler.o(.data*);
      build/data/camera_before.data.o(.data*);
      build/src/camera.o(.data*);
      build/data/camera.data.o(.data*);
      build/src/object_list_processor.o(.data*);
      build/data/object_helpers.data.o(.data*);
      build/data/behavior_actions.data.o(.data*);
      build/src/platform_displacement.o(.data*);
      build/src/spawn_sound.o(.data*);
      build/src/debug.o(.data*);
      build/data/screen_transition.data.o(.data*);
      build/src/shadow.o(.data*);
      build/src/skybox.o(.data*);
      build/src/moving_texture.o(.data*);
      build/src/transparent_texture.o(.data*);
      build/src/paintings.o(.data*);
      build/src/print.o(.data*);
      build/data/ingame_menu.data.o(.data*);
      build/data/ingame_menu_2.data.o(.data*);
      build/src/hud.o(.data*);
      build/src/hud_print.o(.data*);
      build/src/obj_behaviors.o(.data*);
      build/data/obj_behaviors_1.data.o(.data*);
      build/data/obj_behaviors_2.data.o(.data*);
      build/data/audio.data.o(.data*);
      build/data/libultra.data.o(.data*);
      /* rodata */
      build/src/main.o(.rodata*);
      build/src/game.o(.rodata*);
      build/src/sound_init.o(.rodata*);
      build/src/level_update.o(.rodata*);
      build/src/interaction.o(.rodata*);
      build/data/mario.rodata.o(.rodata*);
      build/src/mario_step.o(.rodata*);
      build/data/mario_actions_cutscene.rodata.o(.rodata*);
      build/src/mario_actions_automatic.o(.rodata*);
      build/data/mario_actions_stationary.rodata.o(.rodata*);
      build/src/mario_actions_moving.o(.rodata*);
      build/src/mario_actions_airborne.o(.rodata*);
      build/src/mario_actions_submerged.o(.rodata*);
      build/src/mario_actions_object.o(.rodata*);
      build/src/castle_message_behaviors.o(.rodata*);
      build/src/area.o(.rodata*);
      build/src/rendering_graph_node.o(.rodata*);
      build/src/camera.o(.rodata*);
      build/data/camera.rodata.o(.rodata*);
      build/src/object_list_processor.o(.rodata*);
      build/data/object_helpers.rodata.o(.rodata*);
      build/data/behavior_actions.rodata.o(.rodata*);
      build/src/object_collision.o(.rodata*);
      build/src/spawn_object.o(.rodata*);
      build/src/spawn_sound.o(.rodata*);
      build/src/debug.o(.rodata*);
      build/data/screen_transition.rodata.o(.rodata*);
      build/src/shadow.o(.rodata*);
      build/src/skybox.o(.rodata*);
      build/src/moving_texture.o(.rodata*);
      build/src/paintings.o(.rodata*);
      build/src/print.o(.rodata*);
      build/data/ingame_menu.rodata.o(.rodata*);
      build/data/ingame_menu_2.rodata.o(.rodata*);
      build/src/hud.o(.rodata*);
      build/src/hud_print.o(.rodata*);
      build/src/obj_behaviors.o(.rodata*);
      build/data/obj_behaviors_1.rodata.o(.rodata*);
      build/data/obj_behaviors_2.rodata.o(.rodata*);
      build/data/audio_something.rodata.o(.rodata*);
      build/data/audio.rodata.o(.rodata*);
   }
   END_SEG(main)
   BEGIN_NOLOAD(main)
   {
#if VERSION_US
      . += 0x430;
#endif
      build/src/main.o(.bss*);
      build/src/game.o(.bss*);
      build/data/bss1.o(.bss*);
      build/src/level_update.o(.bss*);
      build/data/bss80339F00.o(.bss*);
      build/src/mario_actions_moving.o(.bss*);
      build/src/mario_actions_submerged.o(.bss*);
      build/data/bss80339FE0.o(.bss*);
      build/src/memory.o(.bss*);
      build/data/bss8033A130.o(.bss*);
      build/src/profiler.o(.bss*);
      build/data/bss8033B020.o(.bss*);
      build/src/object_list_processor.o(.bss*);
      build/data/bss8035FD80.o(.bss*);
      build/src/hud_print.o(.bss*);
      build/data/bss803600E0.o(.bss*);
   }
   END_NOLOAD(main)

   BEGIN_SEG(engine, 0x80378800)
   {
      build/src/math_util.o(.text);
      build/src/graph_node.o(.text);
      build/src/geo_layout.o(.text);
      build/src/level_script.o(.text);
      build/src/surface_collision.o(.text);
      build/src/surface_load.o(.text);
      build/src/behavior_script.o(.text);
      /* data */
      build/data/graph_node.data.o(.data*);
      build/data/math_util.data.o(.data*);
      build/src/geo_layout.o(.data*);
      build/src/level_script.o(.data*);
      build/src/behavior_script.o(.data*);
      /* rodata */
      build/src/math_util.o(.rodata*);
      build/src/level_script.o(.rodata*);
      build/src/surface_collision.o(.rodata*);
      build/src/surface_load.o(.rodata*);
   }
   END_SEG(engine)
   BEGIN_NOLOAD(engine)
   {
      build/data/bss2.o(.bss*);
      build/src/level_script.o(.bss*);
      build/src/surface_collision.o(.bss*);
      build/src/surface_load.o(.bss*);
      build/src/behavior_script.o(.bss*);
   }
   END_NOLOAD(engine)

   /*
    RDRAM begins at 0x8038F800 and ends where
    expansion RAM begins at 0x80400000;
   */
   . = __rdramStart;

   BEGIN_NOLOAD(rdram)
   {
      build/data/rdram.o(.bss*);
   }
   END_NOLOAD(rdram)
   __rdramEnd = .;
   PROVIDE(rdramEnd = .);

   ASSERT((rdramEnd <= 0x80400000), "Error: RDRAM expanded into Expansion RAM, despite Expansion RAM not being defined.")

   BEGIN_SEG(level_main_entry, 0x10000000)
   {
      build/levels/level_main_entry.o(.level);
   }
   END_SEG(level_main_entry)

   /* load MIO0 and level data */
   MIO0_SEG(segment2, 0x02000000)

   STANDARD_OBJECTS(mario_water_sparkles, 0x04000000, 0x17000000)

   STANDARD_OBJECTS(hoot_thwomp_bullet_heave, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(bully_blargg, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(bobombs_bubble, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(sea_creatures, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(klepto_eyerock_pokey, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(mole_monkey_fwoosh, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(spindrift_penguin_snowman, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(capswitch_springboard, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(boo_bookend_chair_piano, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(peach_toadstool_yoshi_birds, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(bubba_wiggler_lakitu, 0x05000000, 0x0C000000)

   STANDARD_OBJECTS(bowser_flames_bomb, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(skeeter_fish_treasurechest, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(koopa_log_piranha_whomp_chomp, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(lakitu_toad_mips_boo, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(chillychief_moneybag, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(mri_swoop_snufit_dorrie_scuttlebug, 0x06000000, 0x0D000000)

   STANDARD_OBJECTS(amp_chuckya_shyguy_goomba_boxes, 0x08000000, 0x0F000000)

   STANDARD_OBJECTS(coins_pipe_doors_maps_trees, 0x03000000, 0x16000000)

   /* use segmented addressing for behaviors */
   BEGIN_SEG(behavior, 0x13000000)
   {
      build/data/behavior_data.o(.behavior);
   }
   END_SEG(behavior)

   /* 0x8016F000 21D7D0-255EC0 [386F0] */
   BEGIN_SEG(goddard, __segCodeSection)
   {
      build/src/level_select_menu.o(.text);
      build/src/intro_geo.o(.text);
      build/src/file_select.o(.text);
      build/src/star_select.o(.text);
      build/src/goddard/gd_main.o(.text);
      build/src/goddard/debug_memory.o(.text);
      build/src/game_over_1.o(.text);
      build/src/goddard/game_over_2.o(.text);
      build/src/goddard/mario_head_1.o(.text);
      build/src/goddard/mario_head_2.o(.text);
      build/src/goddard/mario_head_3.o(.text);
      build/asm/mario_head_4.o(.text);
      build/asm/old_obj_fn.o(.text);
      build/asm/mario_head_5.o(.text);
      build/src/goddard/half_6.o(.text);
      build/asm/mario_head_6.o(.text);
      /* data, rodata, per file */
      build/src/level_select_menu.o(.data*);
      build/src/level_select_menu.o(.rodata*);
      build/src/intro_geo.o(.data*);
      build/src/file_select.o(.data*);
      build/src/file_select.o(.rodata*);
      build/src/star_select.o(.data*);
      build/src/star_select.o(.rodata*);
      build/data/rodata_801A8050.o(.rodata*);
      build/src/goddard/gd_main.o(.rodata*);
      build/src/goddard/debug_memory.o(.rodata*);
      build/src/goddard/game_over_2.o(.rodata*);
      build/src/goddard/mario_head_1.o(.rodata*);
      build/src/goddard/mario_head_2.o(.rodata*);
      build/src/goddard/mario_head_3.o(.rodata*);
      build/data/mario_head_4.rodata.o(.rodata*);
      build/data/old_obj_fn.rodata.o(.rodata*);
      build/data/mario_head_5.rodata.o(.rodata*);
      build/src/goddard/half_6.o(.rodata*);
      build/data/mario_head_6.rodata.o(.rodata*);
   }
   END_SEG(goddard)
   BEGIN_NOLOAD(goddard)
   {
      build/src/intro_geo.o(.bss*);
      build/src/file_select.o(.bss*);
      build/src/star_select.o(.bss*);
      build/src/goddard/gd_main.o(.bss*);
      build/src/goddard/debug_memory.o(.bss*);
      build/src/game_over_1.o(.bss*);
      build/src/goddard/game_over_2.o(.bss*);
      build/src/goddard/mario_head_1.o(.bss*);
      build/src/goddard/mario_head_2.o(.bss*);
      build/src/goddard/mario_head_3.o(.bss*);
      build/data/bss3.o(.bss*);
   }
   END_NOLOAD(goddard)

   . = 0x801C1000; /* stack begin addr */
   BEGIN_NOLOAD(stack)
   {
      build/data/stack.o(.stack);
   }
   END_NOLOAD(stack)

   /* 0x268020 0x268020-0 [0] */
   BEGIN_SEG(level_intro, 0x14000000)
   {
      build/levels/level_intro.o(.level);
   }
   END_SEG(level_intro)
   BEGIN_SEG(level_intro_segment_7, 0x07000000)
   {
      build/levels/level_intro.o(.seg07);
   }
   END_SEG(level_intro_segment_7)
   BEGIN_SEG(mio0_c, __romPos)
   {
      build/data/level_misc.o(.mio0_c);
   }
   END_SEG(mio0_c)

   /* 271B20-2A42A0 [32780] */
   BEGIN_SEG(text271B20, __romPos)
   {
      build/data/unknown_271B20.o(.text);
   }
   END_SEG(text271B20)

   gMainLevels = __romPos;

   BEGIN_SEG(level_main_menu, 0x14000000)
   {
      build/levels/level_main_menu.o(.level);
   }
   END_SEG(level_main_menu)
   BEGIN_SEG(level_main_menu_segment_7, 0x07000000)
   {
      build/levels/level_main_menu.o(.seg07);
   }
   END_SEG(level_main_menu_segment_7)

   BEGIN_SEG(level_main_scripts, 0x15000000)
   {
      build/levels/level_main_scripts.o(.level);
   }
   END_SEG(level_main_scripts)

   MIO0_SEG(water_skybox, 0x0A000000)
   MIO0_SEG(ccm_skybox, 0x0A000000)
   MIO0_SEG(clouds_skybox, 0x0A000000)
   MIO0_SEG(bitfs_skybox, 0x0A000000)
   MIO0_SEG(wdw_skybox, 0x0A000000)
   MIO0_SEG(cloud_floor_skybox, 0x0A000000)
   MIO0_SEG(ssl_skybox, 0x0A000000)
   MIO0_SEG(bbh_skybox, 0x0A000000)
   MIO0_SEG(bidw_skybox, 0x0A000000)
   MIO0_SEG(bits_skybox, 0x0A000000)

   MIO0_SEG(lll_textures, 0x09000000)
   MIO0_SEG(bbh_textures, 0x09000000)
   MIO0_SEG(bob_textures, 0x09000000)
   MIO0_SEG(jrb_textures, 0x09000000)
   MIO0_SEG(rr_textures, 0x09000000)
   MIO0_SEG(ccm_textures, 0x09000000)
   MIO0_SEG(hmc_textures, 0x09000000)
   MIO0_SEG(ttc_textures, 0x09000000)
   MIO0_SEG(ttm_textures, 0x09000000)
   MIO0_SEG(wf_textures, 0x09000000)
   MIO0_SEG(castle_grounds_textures, 0x09000000)
   MIO0_SEG(inside_castle_textures, 0x09000000)
   MIO0_SEG(flower_textures, 0x0B000000)

   STANDARD_LEVEL(level_bbh)
   STANDARD_LEVEL(level_ccm)
   STANDARD_LEVEL(level_castle)
   STANDARD_LEVEL(level_hmc)
   STANDARD_LEVEL(level_ssl)
   STANDARD_LEVEL(level_bob)
   STANDARD_LEVEL(level_sl)
   STANDARD_LEVEL(level_wdw)
   STANDARD_LEVEL(level_jrb)
   STANDARD_LEVEL(level_thi)
   STANDARD_LEVEL(level_ttc)
   STANDARD_LEVEL(level_rr)
   STANDARD_LEVEL(level_castle_grounds)
   STANDARD_LEVEL(level_bitdw)
   STANDARD_LEVEL(level_vcutm)
   STANDARD_LEVEL(level_bitfs)
   STANDARD_LEVEL(level_sa)
   STANDARD_LEVEL(level_bits)
   STANDARD_LEVEL(level_lll)
   STANDARD_LEVEL(level_ddd)
   STANDARD_LEVEL(level_wf)
   STANDARD_LEVEL(level_ending)
   STANDARD_LEVEL(level_castle_courtyard)
   STANDARD_LEVEL(level_pss)
   STANDARD_LEVEL(level_cotmc)
   STANDARD_LEVEL(level_totwc)
   STANDARD_LEVEL(level_bowser_1)
   STANDARD_LEVEL(level_wmotr)
   STANDARD_LEVEL(level_bowser_2)
   STANDARD_LEVEL(level_bowser_3)
   STANDARD_LEVEL(level_ttm)

   /* 4E9FA0-? [?] */
   BEGIN_SEG(assets, __romPos)
   {
      build/anims/mario_anim_data.o(.data);
      . = ALIGN(0x10);
      build/demos/demo_data.o(.data);
      . = ALIGN(0x10);
      build/sound/sound_data.o(.data);
      . = ALIGN(0x10);
      build/sound/music_data.o(.data);
      . = ALIGN(0x10);
   }
   END_SEG(assets)

   /* Discard everything not specifically mentioned above. */
   /DISCARD/ :
   {
      *(*);
   }
}
