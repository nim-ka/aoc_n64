/* Super Mario 64 (J) linker script
 * generated by n64split v0.4a - N64 ROM splitter */

OUTPUT_ARCH (mips)

__segPoolStart = 0x8005C000; /* the segment pool starts here. */
__segCodeSection = 0x8016F000;
__segPoolEnd = 0x801C1000;
__rdramStart = 0x8038F800;
__expansionRamStart = 0x80400000;

#define BEGIN_SEG(name, addr) \
    _##name##SegmentStart = ADDR(.name); \
    _##name##SegmentRomStart = __romPos; \
    .name addr : AT(__romPos)

#define END_SEG(name) \
    _##name##SegmentEnd = ADDR(.name) + SIZEOF(.name); \
    _##name##SegmentRomEnd = __romPos + SIZEOF(.name); \
    __romPos += SIZEOF(.name);

#define BEGIN_NOLOAD(name) \
    _##name##SegmentNoloadStart = ADDR(.name.noload); \
    .name.noload (NOLOAD) :

#define END_NOLOAD(name) \
    _##name##SegmentNoloadEnd = ADDR(.name.noload) + SIZEOF(.name.noload);

#define MIO0_SEG(name, segAddr) \
   BEGIN_SEG(name##_mio0, segAddr) \
   { \
      BUILD_DIR/mio0/name.mio0.o(.data); \
      . = ALIGN(0x10); \
   } \
   END_SEG(name##_mio0)

#define STANDARD_LEVEL(name) \
   BEGIN_SEG(name##_segment_7, 0x07000000) \
   { \
      BUILD_DIR/levels/name.o(.seg07); \
   } \
   END_SEG(name##_segment_7) \
   BEGIN_SEG(name, 0x0E000000) \
   { \
      BUILD_DIR/levels/name.o(.level); \
   } \
   END_SEG(name)

#define STANDARD_OBJECTS(name, segAddr, geoAddr) \
   MIO0_SEG(name, segAddr) \
   BEGIN_SEG(name##_geo, geoAddr) \
   { \
      BUILD_DIR/geo/name.o(.geo); \
      . = ALIGN(0x10); \
   } \
   END_SEG(name##_geo)

SECTIONS
{
   __romPos = 0;

   BEGIN_SEG(boot, 0x04000000)
   {
      BUILD_DIR/asm/rom_header.o(.text);
      BUILD_DIR/asm/boot.o(.text);
   }
   END_SEG(boot)

   BEGIN_SEG(main, 0x80246000)
   {
      BUILD_DIR/asm/entry.o(.text);
      BUILD_DIR/src/main.o(.text);
      BUILD_DIR/src/display.o(.text);
      BUILD_DIR/src/game.o(.text);
      BUILD_DIR/src/sound_init.o(.text);
      BUILD_DIR/src/level_update.o(.text);
      BUILD_DIR/src/interaction.o(.text);
      BUILD_DIR/src/mario.o(.text);
      BUILD_DIR/src/mario_step.o(.text);
      BUILD_DIR/asm/mario_actions_cutscene.o(.text);
      BUILD_DIR/src/mario_actions_automatic.o(.text);
      BUILD_DIR/asm/mario_actions_stationary.o(.text);
      BUILD_DIR/src/mario_actions_moving.o(.text);
      BUILD_DIR/src/mario_actions_airborne.o(.text);
      BUILD_DIR/src/mario_actions_submerged.o(.text);
      BUILD_DIR/src/mario_actions_object.o(.text);
      BUILD_DIR/src/castle_message_behaviors.o(.text);
      BUILD_DIR/src/memory.o(.text);
      BUILD_DIR/src/save_file.o(.text);
      BUILD_DIR/src/area.o(.text);
      BUILD_DIR/src/rendering_graph_node.o(.text);
      BUILD_DIR/src/profiler.o(.text);
      BUILD_DIR/asm/decompress.o(.text);
      BUILD_DIR/src/camera.o(.text);
      BUILD_DIR/asm/camera.o(.text);
      BUILD_DIR/src/object_list_processor.o(.text);
      BUILD_DIR/src/object_helpers.o(.text);
      BUILD_DIR/asm/behavior_actions.o(.text);
      BUILD_DIR/src/platform_displacement.o(.text);
      BUILD_DIR/src/object_collision.o(.text);
      BUILD_DIR/src/spawn_object.o(.text);
      BUILD_DIR/src/spawn_sound.o(.text);
      BUILD_DIR/src/debug.o(.text);
      BUILD_DIR/asm/screen_transition.o(.text);
      BUILD_DIR/src/shadow.o(.text);
      BUILD_DIR/src/skybox.o(.text);
      BUILD_DIR/src/moving_texture.o(.text);
      BUILD_DIR/src/transparent_texture.o(.text);
      BUILD_DIR/src/paintings.o(.text);
      BUILD_DIR/src/print.o(.text);
      BUILD_DIR/src/ingame_menu.o(.text);
      BUILD_DIR/asm/ingame_menu_2.o(.text);
      BUILD_DIR/src/hud.o(.text);
      BUILD_DIR/src/hud_print.o(.text);
      BUILD_DIR/src/obj_behaviors.o(.text);
      BUILD_DIR/asm/obj_behaviors.o(.text);
      BUILD_DIR/src/obj_behaviors_2.o(.text);
      BUILD_DIR/asm/audio_something.o(.text);
      BUILD_DIR/asm/audio_dac.o(.text);
      BUILD_DIR/src/audio/dma.o(.text);
      BUILD_DIR/src/audio/playback.o(.text);
      BUILD_DIR/asm/audio_playback_2.o(.text);
      BUILD_DIR/asm/audio_interface_1.o(.text);
      BUILD_DIR/src/audio/interface_2.o(.text);
#if VERSION_US
      . += 0x40;
#endif
      BUILD_DIR/asm/libultra.o(.text);
      /* data */
      BUILD_DIR/data/data_8032B3F0.o(.data*);
      BUILD_DIR/src/main.o(.data*);
      BUILD_DIR/src/display.o(.data*);
      BUILD_DIR/src/game.o(.data*);
      BUILD_DIR/src/sound_init.o(.data*);
      BUILD_DIR/src/level_update.o(.data*);
      BUILD_DIR/src/interaction.o(.data*);
      BUILD_DIR/src/mario.o(.data*);
      BUILD_DIR/src/mario_step.o(.data*);
      BUILD_DIR/data/mario_actions_cutscene.data.o(.data*);
      BUILD_DIR/src/mario_actions_moving.o(.data*);
      BUILD_DIR/src/mario_actions_submerged.o(.data*);
      BUILD_DIR/src/mario_actions_object.o(.data*);
      BUILD_DIR/src/castle_message_behaviors.o(.data*);
      BUILD_DIR/src/memory.o(.data*);
      BUILD_DIR/src/save_file.o(.data*);
      BUILD_DIR/data/save_file.data.o(.data*);
      BUILD_DIR/src/area.o(.data*);
      BUILD_DIR/src/rendering_graph_node.o(.data*);
      BUILD_DIR/data/rendering_graph_node.data.o(.data*);
      BUILD_DIR/src/profiler.o(.data*);
      BUILD_DIR/data/camera_before.data.o(.data*);
      BUILD_DIR/src/camera.o(.data*);
      BUILD_DIR/data/camera.data.o(.data*);
      BUILD_DIR/src/object_list_processor.o(.data*);
      BUILD_DIR/src/object_helpers.o(.data*);
      BUILD_DIR/data/behavior_actions.data.o(.data*);
      BUILD_DIR/src/platform_displacement.o(.data*);
      BUILD_DIR/src/spawn_sound.o(.data*);
      BUILD_DIR/src/debug.o(.data*);
      BUILD_DIR/data/screen_transition.data.o(.data*);
      BUILD_DIR/src/shadow.o(.data*);
      BUILD_DIR/src/skybox.o(.data*);
      BUILD_DIR/src/moving_texture.o(.data*);
      BUILD_DIR/src/transparent_texture.o(.data*);
      BUILD_DIR/src/paintings.o(.data*);
      BUILD_DIR/src/print.o(.data*);
      BUILD_DIR/src/ingame_menu.o(.data*);
      BUILD_DIR/data/ingame_menu_2.data.o(.data*);
      BUILD_DIR/src/hud.o(.data*);
      BUILD_DIR/src/hud_print.o(.data*);
      BUILD_DIR/src/obj_behaviors.o(.data*);
      BUILD_DIR/data/obj_behaviors_1.data.o(.data*);
      BUILD_DIR/src/obj_behaviors_2.o(.data*);
      BUILD_DIR/src/audio/interface_2.o(.data*);
      BUILD_DIR/data/audio.data.o(.data*);
      BUILD_DIR/data/libultra.data.o(.data*);
      /* rodata */
      BUILD_DIR/src/main.o(.rodata*);
      BUILD_DIR/src/game.o(.rodata*);
      BUILD_DIR/src/sound_init.o(.rodata*);
      BUILD_DIR/src/level_update.o(.rodata*);
      BUILD_DIR/src/interaction.o(.rodata*);
      BUILD_DIR/src/mario.o(.rodata*);
      BUILD_DIR/src/mario_step.o(.rodata*);
      BUILD_DIR/data/mario_actions_cutscene.rodata.o(.rodata*);
      BUILD_DIR/src/mario_actions_automatic.o(.rodata*);
      BUILD_DIR/data/mario_actions_stationary.rodata.o(.rodata*);
      BUILD_DIR/src/mario_actions_moving.o(.rodata*);
      BUILD_DIR/src/mario_actions_airborne.o(.rodata*);
      BUILD_DIR/src/mario_actions_submerged.o(.rodata*);
      BUILD_DIR/src/mario_actions_object.o(.rodata*);
      BUILD_DIR/src/castle_message_behaviors.o(.rodata*);
      BUILD_DIR/src/area.o(.rodata*);
      BUILD_DIR/src/rendering_graph_node.o(.rodata*);
      BUILD_DIR/src/camera.o(.rodata*);
      BUILD_DIR/data/camera.rodata.o(.rodata*);
      BUILD_DIR/src/object_list_processor.o(.rodata*);
      BUILD_DIR/src/object_helpers.o(.rodata*);
      BUILD_DIR/data/behavior_actions.rodata.o(.rodata*);
      BUILD_DIR/src/object_collision.o(.rodata*);
      BUILD_DIR/src/spawn_object.o(.rodata*);
      BUILD_DIR/src/spawn_sound.o(.rodata*);
      BUILD_DIR/src/debug.o(.rodata*);
      BUILD_DIR/data/screen_transition.rodata.o(.rodata*);
      BUILD_DIR/src/shadow.o(.rodata*);
      BUILD_DIR/src/skybox.o(.rodata*);
      BUILD_DIR/src/moving_texture.o(.rodata*);
      BUILD_DIR/src/paintings.o(.rodata*);
      BUILD_DIR/src/print.o(.rodata*);
      BUILD_DIR/src/ingame_menu.o(.rodata*);
      BUILD_DIR/data/ingame_menu_2.rodata.o(.rodata*);
      BUILD_DIR/src/hud.o(.rodata*);
      BUILD_DIR/src/hud_print.o(.rodata*);
      BUILD_DIR/src/obj_behaviors.o(.rodata*);
      BUILD_DIR/data/obj_behaviors_1.rodata.o(.rodata*);
      BUILD_DIR/src/obj_behaviors_2.o(.rodata*);
      BUILD_DIR/data/audio_something.rodata.o(.rodata*);
      BUILD_DIR/data/audio_dac.rodata.o(.rodata*);
      BUILD_DIR/src/audio/playback.o(.rodata*);
      BUILD_DIR/data/audio.rodata.o(.rodata*);
      BUILD_DIR/src/audio/interface_2.o(.rodata*);
      BUILD_DIR/data/libultra.rodata.o(.rodata*);
   }
   END_SEG(main)
   BEGIN_NOLOAD(main)
   {
      BUILD_DIR/src/main.o(.bss*);
      BUILD_DIR/src/game.o(.bss*);
      BUILD_DIR/data/bss1.o(.bss*);
      BUILD_DIR/src/level_update.o(.bss*);
      BUILD_DIR/data/bss80339F00.o(.bss*);
      BUILD_DIR/src/mario_actions_moving.o(.bss*);
      BUILD_DIR/src/mario_actions_submerged.o(.bss*);
      BUILD_DIR/data/bss80339FE0.o(.bss*);
      BUILD_DIR/src/memory.o(.bss*);
      BUILD_DIR/data/bss8033A130.o(.bss*);
      BUILD_DIR/src/profiler.o(.bss*);
      BUILD_DIR/data/bss8033B020.o(.bss*);
      BUILD_DIR/src/object_list_processor.o(.bss*);
      BUILD_DIR/data/bss8035FD80.o(.bss*);
      BUILD_DIR/src/hud_print.o(.bss*);
      BUILD_DIR/data/bss803600E0.o(.bss*);
      BUILD_DIR/src/audio/interface_2.o(.bss*);
      BUILD_DIR/data/libultra.bss.o(.bss*);
   }
   END_NOLOAD(main)

   BEGIN_SEG(engine, 0x80378800)
   {
      BUILD_DIR/src/math_util.o(.text);
      BUILD_DIR/src/graph_node.o(.text);
      BUILD_DIR/src/geo_layout.o(.text);
      BUILD_DIR/src/level_script.o(.text);
      BUILD_DIR/src/surface_collision.o(.text);
      BUILD_DIR/src/surface_load.o(.text);
      BUILD_DIR/src/behavior_script.o(.text);
      /* data */
      BUILD_DIR/data/graph_node.data.o(.data*);
      BUILD_DIR/data/math_util.data.o(.data*);
      BUILD_DIR/src/geo_layout.o(.data*);
      BUILD_DIR/src/level_script.o(.data*);
      BUILD_DIR/src/behavior_script.o(.data*);
      /* rodata */
      BUILD_DIR/src/math_util.o(.rodata*);
      BUILD_DIR/src/level_script.o(.rodata*);
      BUILD_DIR/src/surface_collision.o(.rodata*);
      BUILD_DIR/src/surface_load.o(.rodata*);
   }
   END_SEG(engine)
   BEGIN_NOLOAD(engine)
   {
      BUILD_DIR/data/bss2.o(.bss*);
      BUILD_DIR/src/level_script.o(.bss*);
      BUILD_DIR/src/surface_collision.o(.bss*);
      BUILD_DIR/src/surface_load.o(.bss*);
      BUILD_DIR/src/behavior_script.o(.bss*);
   }
   END_NOLOAD(engine)

   /*
    RDRAM begins at 0x8038F800 and ends where
    expansion RAM begins at 0x80400000;
   */
   . = __rdramStart;

   BEGIN_NOLOAD(rdram)
   {
      BUILD_DIR/data/rdram.o(.bss*);
   }
   END_NOLOAD(rdram)
   __rdramEnd = .;
   PROVIDE(rdramEnd = .);

   ASSERT((rdramEnd <= 0x80400000), "Error: RDRAM expanded into Expansion RAM, despite Expansion RAM not being defined.")

   BEGIN_SEG(level_main_entry, 0x10000000)
   {
      BUILD_DIR/levels/level_main_entry.o(.level);
   }
   END_SEG(level_main_entry)

   /* load MIO0 and level data */
   MIO0_SEG(segment2, 0x02000000)

   STANDARD_OBJECTS(mario_water_sparkles, 0x04000000, 0x17000000)

   STANDARD_OBJECTS(hoot_thwomp_bullet_heave, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(bully_blargg, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(bobombs_bubble, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(sea_creatures, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(klepto_eyerok_pokey, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(mole_ukiki_fwoosh, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(spindrift_penguin_snowman, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(capswitch_springboard, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(boo_bookend_chair_piano, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(peach_toadstool_yoshi_birds, 0x05000000, 0x0C000000)
   STANDARD_OBJECTS(bubba_wiggler_lakitu, 0x05000000, 0x0C000000)

   STANDARD_OBJECTS(bowser_flames_bomb, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(skeeter_fish_treasurechest, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(koopa_log_piranha_whomp_chomp, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(lakitu_toad_mips_boo, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(chillychief_moneybag, 0x06000000, 0x0D000000)
   STANDARD_OBJECTS(mr_i_swoop_snufit_dorrie_scuttlebug, 0x06000000, 0x0D000000)

   STANDARD_OBJECTS(amp_chuckya_flyguy_goomba_boxes, 0x08000000, 0x0F000000)

   STANDARD_OBJECTS(coins_pipe_doors_maps_trees, 0x03000000, 0x16000000)

   /* use segmented addressing for behaviors */
   BEGIN_SEG(behavior, 0x13000000)
   {
      BUILD_DIR/data/behavior_data.o(.behavior);
   }
   END_SEG(behavior)

   /* 0x8016F000 21D7D0-255EC0 [386F0] */
   BEGIN_SEG(goddard, __segCodeSection)
   {
      BUILD_DIR/src/level_select_menu.o(.text);
      BUILD_DIR/src/intro_geo.o(.text);
      BUILD_DIR/src/file_select.o(.text);
      BUILD_DIR/src/star_select.o(.text);
      BUILD_DIR/src/goddard/gd_main.o(.text);
      BUILD_DIR/src/goddard/debug_memory.o(.text);
      BUILD_DIR/src/goddard/game_over_1.o(.text);
      BUILD_DIR/src/goddard/game_over_2.o(.text);
      BUILD_DIR/src/goddard/mario_head_1.o(.text);
      BUILD_DIR/src/goddard/mario_head_2.o(.text);
      BUILD_DIR/src/goddard/mario_head_3.o(.text);
      BUILD_DIR/src/goddard/mario_head_4.o(.text);
      BUILD_DIR/src/goddard/old_obj_fn.o(.text);
      BUILD_DIR/src/goddard/profiler_utils.o(.text);
      BUILD_DIR/src/goddard/joint_fns.o(.text);
      BUILD_DIR/src/goddard/skin_fns.o(.text);
      BUILD_DIR/src/goddard/matrix_fns.o(.text);
      BUILD_DIR/src/goddard/half_6.o(.text);
      BUILD_DIR/src/goddard/mario_head_6.o(.text);
      BUILD_DIR/asm/mario_head_6.o(.text);
      /* data, rodata, per file */
      BUILD_DIR/src/level_select_menu.o(.data*);
      BUILD_DIR/src/level_select_menu.o(.rodata*);
      BUILD_DIR/src/intro_geo.o(.data*);
      BUILD_DIR/src/file_select.o(.data*);
      BUILD_DIR/src/file_select.o(.rodata*);
      BUILD_DIR/src/star_select.o(.data*);
      BUILD_DIR/src/star_select.o(.rodata*);
      /* goddard subsystem data */
      BUILD_DIR/src/goddard/gd_main.o(.data*);
      BUILD_DIR/src/goddard/game_over_2.o(.data*);
      BUILD_DIR/src/goddard/mario_head_1.o(.data*);
      BUILD_DIR/src/goddard/mario_head_3.o(.data*);
      BUILD_DIR/src/goddard/mario_head_4.o(.data*);
      BUILD_DIR/src/goddard/profiler_utils.o(.data*);
      BUILD_DIR/src/goddard/joint_fns.o(.data*);
      BUILD_DIR/src/goddard/half_6.o(.data*);
      BUILD_DIR/data/mario_head_6.data.o(.data*);
      /* goddard texture and DLs */
      BUILD_DIR/data/goddard_tex_dl.o(.rodata*);
      /* goddard subsystem rodata */
      BUILD_DIR/src/goddard/gd_main.o(.rodata*);
      BUILD_DIR/src/goddard/debug_memory.o(.rodata*);
      BUILD_DIR/src/goddard/game_over_2.o(.rodata*);
      BUILD_DIR/src/goddard/mario_head_1.o(.rodata*);
      BUILD_DIR/src/goddard/mario_head_2.o(.rodata*);
      BUILD_DIR/src/goddard/mario_head_3.o(.rodata*);
      BUILD_DIR/src/goddard/mario_head_4.o(.rodata*);
      BUILD_DIR/src/goddard/old_obj_fn.o(.rodata*);
      BUILD_DIR/src/goddard/profiler_utils.o(.rodata*);
      BUILD_DIR/src/goddard/joint_fns.o(.rodata*);
      BUILD_DIR/src/goddard/skin_fns.o(.rodata*);
      BUILD_DIR/src/goddard/matrix_fns.o(.rodata*);
      BUILD_DIR/src/goddard/half_6.o(.rodata*);
      BUILD_DIR/data/mario_head_6.rodata.o(.rodata*);
   }
   END_SEG(goddard)
   BEGIN_NOLOAD(goddard)
   {
      BUILD_DIR/src/intro_geo.o(.bss*);
      BUILD_DIR/src/file_select.o(.bss*);
      BUILD_DIR/src/star_select.o(.bss*);
      BUILD_DIR/src/goddard/gd_main.o(.bss*);
      BUILD_DIR/src/goddard/debug_memory.o(.bss*);
      BUILD_DIR/src/goddard/game_over_1.o(.bss*);
      BUILD_DIR/src/goddard/game_over_2.o(.bss*);
      BUILD_DIR/src/goddard/mario_head_1.o(.bss*);
      BUILD_DIR/src/goddard/mario_head_2.o(.bss*);
      BUILD_DIR/src/goddard/mario_head_3.o(.bss*);
      BUILD_DIR/src/goddard/mario_head_4.o(.bss*);
      BUILD_DIR/src/goddard/old_obj_fn.o(.bss*);
      BUILD_DIR/src/goddard/profiler_utils.o(.bss*);
      BUILD_DIR/src/goddard/joint_fns.o(.bss*);
      BUILD_DIR/src/goddard/skin_fns.o(.bss*);
      BUILD_DIR/src/goddard/half_6.o(.bss*);
      BUILD_DIR/data/bss3.o(.bss*);
   }
   END_NOLOAD(goddard)

   . = 0x801C1000; /* stack begin addr */
   BEGIN_NOLOAD(stack)
   {
      BUILD_DIR/data/stack.o(.stack);
   }
   END_NOLOAD(stack)

   /* 0x268020 0x268020-0 [0] */
   BEGIN_SEG(level_intro, 0x14000000)
   {
      BUILD_DIR/levels/level_intro.o(.level);
   }
   END_SEG(level_intro)
   BEGIN_SEG(level_intro_segment_7, 0x07000000)
   {
      BUILD_DIR/levels/level_intro.o(.seg07);
   }
   END_SEG(level_intro_segment_7)
   MIO0_SEG(debug_level_select, 0x07000000)
   MIO0_SEG(title_screen_bg, 0x0A000000)

   /* 271B20-2A42A0 [32780] */
   BEGIN_SEG(gd_dynlists, 0x04000000)
   {
      BUILD_DIR/src/goddard/dynlists/04000000.o(.data);
      BUILD_DIR/src/goddard/dynlists/04000650.o(.data);
      BUILD_DIR/src/goddard/dynlists/04000C20.o(.data);
      BUILD_DIR/src/goddard/dynlists/04003610.o(.data);
      BUILD_DIR/src/goddard/dynlists/040042E0.o(.data);
      BUILD_DIR/src/goddard/dynlists/04004F90.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_0C6DC.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_0DE1C.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_0F55C.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_10C9C.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_123DC.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_13738.o(.data);
      BUILD_DIR/src/goddard/dynlists/anim_22768.o(.data);
      BUILD_DIR/src/goddard/dynlists/04000000.o(.rodata);
      BUILD_DIR/src/goddard/dynlists/04000650.o(.rodata);
   }
   END_SEG(gd_dynlists)

   gMainLevels = __romPos;

   BEGIN_SEG(level_main_menu, 0x14000000)
   {
      BUILD_DIR/levels/level_main_menu.o(.level);
   }
   END_SEG(level_main_menu)
   BEGIN_SEG(level_main_menu_segment_7, 0x07000000)
   {
      BUILD_DIR/levels/level_main_menu.o(.seg07);
   }
   END_SEG(level_main_menu_segment_7)

   BEGIN_SEG(level_main_scripts, 0x15000000)
   {
      BUILD_DIR/levels/level_main_scripts.o(.level);
   }
   END_SEG(level_main_scripts)

   MIO0_SEG(water_skybox, 0x0A000000)
   MIO0_SEG(ccm_skybox, 0x0A000000)
   MIO0_SEG(clouds_skybox, 0x0A000000)
   MIO0_SEG(bitfs_skybox, 0x0A000000)
   MIO0_SEG(wdw_skybox, 0x0A000000)
   MIO0_SEG(cloud_floor_skybox, 0x0A000000)
   MIO0_SEG(ssl_skybox, 0x0A000000)
   MIO0_SEG(bbh_skybox, 0x0A000000)
   MIO0_SEG(bidw_skybox, 0x0A000000)
   MIO0_SEG(bits_skybox, 0x0A000000)

   MIO0_SEG(lll_textures, 0x09000000)
   MIO0_SEG(bbh_textures, 0x09000000)
   MIO0_SEG(bob_textures, 0x09000000)
   MIO0_SEG(jrb_textures, 0x09000000)
   MIO0_SEG(rr_textures, 0x09000000)
   MIO0_SEG(ccm_textures, 0x09000000)
   MIO0_SEG(hmc_textures, 0x09000000)
   MIO0_SEG(ttc_textures, 0x09000000)
   MIO0_SEG(ttm_textures, 0x09000000)
   MIO0_SEG(wf_textures, 0x09000000)
   MIO0_SEG(castle_grounds_textures, 0x09000000)
   MIO0_SEG(inside_castle_textures, 0x09000000)
   MIO0_SEG(flower_bubbles_textures, 0x0B000000)

   STANDARD_LEVEL(level_bbh)
   STANDARD_LEVEL(level_ccm)
   STANDARD_LEVEL(level_castle)
   STANDARD_LEVEL(level_hmc)
   STANDARD_LEVEL(level_ssl)
   STANDARD_LEVEL(level_bob)
   STANDARD_LEVEL(level_sl)
   STANDARD_LEVEL(level_wdw)
   STANDARD_LEVEL(level_jrb)
   STANDARD_LEVEL(level_thi)
   STANDARD_LEVEL(level_ttc)
   STANDARD_LEVEL(level_rr)
   STANDARD_LEVEL(level_castle_grounds)
   STANDARD_LEVEL(level_bitdw)
   STANDARD_LEVEL(level_vcutm)
   STANDARD_LEVEL(level_bitfs)
   STANDARD_LEVEL(level_sa)
   STANDARD_LEVEL(level_bits)
   STANDARD_LEVEL(level_lll)
   STANDARD_LEVEL(level_ddd)
   STANDARD_LEVEL(level_wf)
   STANDARD_LEVEL(level_ending)
   STANDARD_LEVEL(level_castle_courtyard)
   STANDARD_LEVEL(level_pss)
   STANDARD_LEVEL(level_cotmc)
   STANDARD_LEVEL(level_totwc)
   STANDARD_LEVEL(level_bowser_1)
   STANDARD_LEVEL(level_wmotr)
   STANDARD_LEVEL(level_bowser_2)
   STANDARD_LEVEL(level_bowser_3)
   STANDARD_LEVEL(level_ttm)

   /* 4E9FA0-? [?] */
   BEGIN_SEG(assets, __romPos)
   {
      BUILD_DIR/anims/mario_anim_data.o(.data);
      . = ALIGN(0x10);
      BUILD_DIR/demos/demo_data.o(.data);
      . = ALIGN(0x10);
      BUILD_DIR/sound/sound_data.o(.data);
      . = ALIGN(0x10);
      BUILD_DIR/sound/music_data.o(.data);
      . = ALIGN(0x10);
   }
   END_SEG(assets)

   /* Discard everything not specifically mentioned above. */
   /DISCARD/ :
   {
      *(*);
   }
}
